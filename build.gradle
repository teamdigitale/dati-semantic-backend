plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.1.4'
    id 'checkstyle'
    id 'jacoco'
    id "org.owasp.dependencycheck" version "6.1.6"
    id "com.github.spotbugs" version "4.5.1"
}

group = 'it.team-digitale'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
    integrationCompileOnly.extendsFrom testCompileOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'org.springframework.data', name: 'spring-data-elasticsearch', version: '4.2.6'
    implementation 'org.apache.jena:apache-jena-libs:4.2.0'
    implementation 'org.apache.jena:jena-querybuilder:4.2.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.12'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    integrationCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation 'io.rest-assured:rest-assured:4.0.0'
    testImplementation 'io.rest-assured:json-path:4.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.mockito:mockito-inline:3.9.0'

    testImplementation "org.testcontainers:elasticsearch:1.16.2"
    testImplementation "org.testcontainers:testcontainers:1.16.0"
    testImplementation "org.testcontainers:junit-jupiter:1.16.0"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

test {
    useJUnitPlatform()
}

jib {
    from {
        image = 'gcr.io/distroless/java:11-nonroot'
    }
}

checkstyle {
    toolVersion = '9.0.1'
    maxWarnings = 0
    ignoreFailures false
    configDirectory = file("${rootProject.projectDir}/config/checkstyle")
}

jacocoTestReport {
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    violationRules {

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
            excludes = [
                    'it.teamdigitale.ndc.config.*',
                    'it.teamdigitale.ndc.Application',
                    'it.teamdigitale.ndc.harvester.util.*',
                    'it.teamdigitale.ndc.controller.ScheduledJobsController',
            ]
        }
    }
}

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencyCheck {
//set up a quality gate for vulnerabilities with high severity level:
//let's consider that a vulnerability has a high severity level if its CVSS score is higher than 7
//the build is going to fail if vulnerabilities with high severity level found
    failBuildOnCVSS = 7

//specify a list of known issues which contain:
//false-positives
//confirmed vulnerabilities which are not fixed yet, but we have a ticket for that
    suppressionFile = 'config/dependency-check/dependency-check-known-issues.xml'
}

task copyPrecommitHooks(type: Exec) {
    workingDir "$projectDir"
    commandLine 'sh', 'config/hooks/copy-precommit-hooks.sh'
}

build.finalizedBy copyPrecommitHooks

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(build)) {
        spotbugsMain.enabled = false
        dependencyCheckAnalyze.enabled = false
        spotbugsTest.enabled = false
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

integrationTest.finalizedBy jacocoTestReport
check.dependsOn integrationTest
jacocoTestCoverageVerification.dependsOn integrationTest