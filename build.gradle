plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.1.4'
    id 'checkstyle'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '6.5.3'
    id 'com.github.spotbugs' version '4.5.1'
    id 'org.openapi.generator' version '5.3.0'
    id 'idea'
}

group = 'it.gov.innovazione'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
    integrationCompileOnly.extendsFrom testCompileOnly
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-batch'

    implementation 'org.springframework.data:spring-data-elasticsearch:4.3.2'
    implementation 'org.apache.jena:apache-jena-libs:4.4.0'
    implementation 'org.apache.jena:jena-querybuilder:4.4.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'


    implementation 'org.springdoc:springdoc-openapi-ui:1.6.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'io.swagger:swagger-annotations:1.5.20'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'

    runtimeOnly 'mysql:mysql-connector-java:8.0.26'

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    integrationCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    testImplementation 'io.rest-assured:rest-assured:4.0.0'
    testImplementation 'io.rest-assured:json-path:4.0.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.mockito:mockito-inline:3.9.0'

    integrationImplementation "org.testcontainers:elasticsearch:1.16.2"
    integrationImplementation "org.testcontainers:testcontainers:1.16.2"
    integrationImplementation "org.testcontainers:junit-jupiter:1.16.2"
    integrationImplementation "com.h2database:h2:2.1.210"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
}

ext['log4j2.version'] = '2.17.2'
ext['logback.version'] = '1.2.9'

test {
    useJUnitPlatform()
}

jib {
    from {
        image = 'gcr.io/distroless/java:11-nonroot'
    }
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/src/main/resources/public/api-docs.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = 'it.gov.innovazione.ndc.gen.api'
    invokerPackage = 'it.gov.innovazione.ndc.gen.invoker'
    modelPackage = 'it.gov.innovazione.ndc.gen.dto'
    configOptions = [
            library              : 'spring-boot',
            performBeanValidation: 'true',
            dateLibrary          : 'java8',
            title                : 'National Data Catalog API',
            interfaceOnly        : 'true'
    ]
}

sourceSets.main.java.srcDir new File(buildDir, 'generated/src/main/java')
idea {
    module {
        generatedSourceDirs += new File(buildDir, 'generated/src/main/java')
    }
}

checkstyle {
    toolVersion = '9.0.1'
    maxWarnings = 0
    ignoreFailures false
    configDirectory = file("${rootProject.projectDir}/config/checkstyle")
}

tasks.withType(Checkstyle) {
    exclude "**/gen/**"
}

jacocoTestReport {
    sourceSets sourceSets.main
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    reports {
        xml.required = false
        csv.required = false
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    violationRules {

        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.9
            }
            excludes = [
                    'it.gov.innovazione.ndc.config.*',
                    'it.gov.innovazione.ndc.Application',
                    'it.gov.innovazione.ndc.harvester.util.*',
                    'it.gov.innovazione.ndc.model.profiles.*',
                    'it.gov.innovazione.ndc.gen.*',
            ]
        }
    }
}

sourceSets {
    integration {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
compileJava {
    dependsOn tasks.openApiGenerate
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring'
    ]
}

spotbugsMain {
    excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    excludeFilter = file("${rootProject.projectDir}/config/spotbugs/exclude-filter.xml")
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

dependencyCheck {
//set up a quality gate for vulnerabilities with high severity level:
//let's consider that a vulnerability has a high severity level if its CVSS score is higher than 7
//the build is going to fail if vulnerabilities with high severity level found
    failBuildOnCVSS = 7

//specify a list of known issues which contain:
//false-positives
//confirmed vulnerabilities which are not fixed yet, but we have a ticket for that
    suppressionFile = 'config/dependency-check/dependency-check-known-issues.xml'
}

task copyPrecommitHooks(type: Exec) {
    workingDir "$projectDir"
    commandLine 'sh', 'config/hooks/copy-precommit-hooks.sh'
}

build.finalizedBy copyPrecommitHooks

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(build)) {
        spotbugsMain.enabled = false
        dependencyCheckAnalyze.enabled = false
        spotbugsTest.enabled = false
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

integrationTest.finalizedBy jacocoTestReport
check.dependsOn integrationTest
jacocoTestCoverageVerification.dependsOn integrationTest